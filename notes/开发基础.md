# 1 数据模型

单一、集成的数据字典，可使用多种数据模型之一进行访问。数据可以建模并存储为表、对象、多维数组或文档。

不同的模型可以无缝访问相同的数据，而无需在模型之间进行性能降低的映射，并且可以同时对相同的数据进行完全并发使用。

- 多维访问：所有数据都存储在高效的、基于树的稀疏多维数组（称为全局变量）中。可提供非常快速、灵活的存储和检索。

  **InterSystems IRIS 支持一种在其他编程语言中看不到的特殊变量 - 全局变量。** *“全局”*表示此数据可供访问此数据库的所有进程使用。这种用法与其他编程语言不同，在其他编程语言中，*“全局”*表示“可供此模块中的所有代码使用”。全局变量的内容存储在 InterSystems IRIS 数据库中。数据库只包含全局变量，不包含其他内容；甚至代码也存储在全局变量中。底层所有数据访问都是通过直接全局访问完成的。

```java
/// Create a custom data structure to store airfare in a graph-like structure and retrieve airfare based on nodes
/// Takes departure airport and arrival airport as arguments
ClassMethod StoreAirfare() As %Status
{
	set ^airport("BOS","AUS") = 1698
	set ^airport("BOS","AUS","AA150") = 450
	set ^airport("BOS","AUS","AA290") = 550
	
	set ^airport("BOS","PHL") = 280
	set ^airport("BOS","PHL","UA110") = 200
	
	set ^airport("BOS","BIS") = 1490
	set ^airport("BOS","BIS","AA330") = 700
	set ^airport("BOS","BIS","UA208") = 710
	
	write "Stored fare and distance data in ^airport global."
	quit $$$OK
}

ClassMethod CheckAirfare(fromAirport As %String, toAirport As %String) As %Status
{
    write !,"The distance in miles between " _ fromAirport _ " and "_ toAirport _ " is: " _ $get(^airport(fromAirport, toAirport)) 
    set isDefined = $data(^airport(fromAirport,toAirport))
    if ((isDefined = 11)) { 
    	write !,"The following routes exist for this path:"
    	set flightNumber = ""
    	while ($order(^airport(fromAirport,toAirport, flightNumber)) '= ""){
    		set flightNumber = $order(^airport(fromAirport,toAirport, flightNumber))
    		set fare = ^airport(fromAirport,toAirport, flightNumber)
    		write !,"  - " _ flightNumber _ ": " _ fare _ " USD"
    	}
    }
    else{
	    write "No routes exist for this path."
	}
}
```



- 对象访问：提供具有多重继承和其他功能的复杂对象模型。有多种方法创建、存储和操作对象。

```java
/// Store objects directly to the database
ClassMethod PopulateAirports() As %Status
{
	//1. Boston
	set newAirport = ##class(DataModels.Airport).%New()
	set newAirport.Name = "Boston Logan International"
	set newAirport.Code = "BOS"
	set newAirport.Location = ##class(DataModels.Location).%New()
	set newAirport.Location.City = "Boston"
	set newAirport.Location.State = "MA"
	do newAirport.%Save()
	
	//2. Philadephia
	set newAirport = ##class(DataModels.Airport).%New()
	set newAirport.Name = "Philadephia International"
	set newAirport.Code = "PHL"
	set newAirport.Location = ##class(DataModels.Location).%New()
	set newAirport.Location.City = "Philadelphia"
	set newAirport.Location.State = "PA"
	do newAirport.%Save()
	
	quit $$$OK
}
```



- SQL访问：过ODBC和JDBC提供高性能访问。它还提供关系网关，以方便从关系数据库导入数据。

```java
/// Display all airports using cursor-based embedded SQL
ClassMethod DisplayAirports() As %Status
{
	//1. 动态SQL
	set myquery = "SELECT Name, Code, Location->City, Location->State FROM DataModels.Airport"
	set tStatement = ##class(%SQL.Statement).%New()
	set tStatus = tStatement.%Prepare(myquery)
	set rset = tStatement.%Execute()
	do rset.%Display()
	WRITE !,"End of data"

	//2. 嵌入式SQL
	&sql(DECLARE c1 CURSOR FOR
       	SELECT Name, Code, Location->City, Location->State INTO :name, :code, :city, :state
       	FROM DataModels.Airport)
       	
	&sql(OPEN c1)
	&sql(FETCH c1)
	write "Airport",?35,"Code",?45,"Location",!
	while (SQLCODE = 0) {
		write name,?35,code,?45, city,",",state,!
	   	&sql(FETCH c1) 
	}
	&sql(CLOSE c1)
		
	quit $$$OK
}
```



**持久类：Basics.Models.Airport，Basics.Models.Location**

```java
Class Basics.Models.Airport Extends %Persistent [ DdlAllowed ]
{

Property Name As %String;

Property Code As %String;

Property Location As Basics.Models.Location;

}

Class Basics.Models.Location Extends %Persistent [ DdlAllowed ]
{

Property City As %String;

Property State As %String;

}
```

# 2 ObjectScript

**ObjectScript** 是 IRIS数据平台中内置的完全通用的编程语言。可在 InterSystems IRIS 支持的所有平台上完全移植。

**ObjectScript**源代码被编译成在 IRIS 虚拟机中执行的目标代码，针对业务应用程序中常见的操作进行了高度优化，包括字符串操作和数据库访问。

## 2.1 语法

```java
ROUTINE Basics.Grammars
#define StringMacro "Hello, World!" // 使用 $$$StringMacro
DataTypes
    /* 数据类型 */
    //字符串
    Set string = "" // 空字符串
    Set string = $CHAR(0) // 空，十六进制值为 00 的字符
    Set string = "This string has ""quotes"" in it." // 转义引号
    Write string,!
    Set a = "Inter"
    Set b = "Systems"
    Set string = a_b //字符串连接
    Write string,!
    Write $EXTRACT("123456", 2, 4),! // 234 截取
    Write $LENGTH("a^b^c^d","^"),! // 4 长度
    Write $PIECE("a^b^c^d", "^", 2),! // b 分隔符取值
    Write $PIECE("a^b^c^d", "^", 2, 3),! // b^c 分隔符取值
    Write $FIND("Once upon a time...", "upon"),! // 搜索字符串，并返回后面字符的位置; 未找到返回0
    Write $REPLACE("red black red wite", "red", "green"),! // green black green wite 字符串替换
    Write $TRANSLATE("red black red wite", "rbw", "RBW"),! // Red Black Red Wite 逐字符替换
    Write $ZCONVERT("abc","U"),! // ABC 转大写
    Write $ZCONVERT("ABC","L"),! // abc 转小写
    Write $ZCONVERT("中文","O","UTF8"),! // 输出编码转换
    Write $ZCONVERT($ZCONVERT("中文","O","UTF8"),"I","UTF8"),! // 输入编码转换

    //数字
    Write +"0000.7dwarves",! // .7
    Write "7dwares" + 2,! // 9
    Write "+24/7" + 2,! // 26
    Write "7.2.34" + 2,! // 9.2
    Write 3e4,! // 30000
    Write $FNUMBER(".25198", "", 3),! // 0.252 格式化 四舍五入
    Write $FNUMBER("-6.25198", "-", 3),! // 6.252 绝对值
    Write $JUSTIFY("6.25198", "", 3),! // 6.252 四舍五入
   
    //对象
    Set newAirport = ##class(Basics.Models.Airport).%New()
    Write newAirport,!
    Write $ISOBJECT(newAirport),! // 是否是对象

    //持久多维数组（全局）
    Set ^x("a") = 1
    Set ^x("a","b") = 2
    Set ^y("a") = 1
    ZWrite ^x("a") // ^x("a")=1 ^x("a","b")=2
    Write $DATA(^x("a")),! // 11 存在有数据且有后代
    Write $DATA(^x("a","b")),! // 1 存在且有数据，没有后代
    Write $DATA(^y),! // 10 有后代,但本身不包含数据。引用本身会UNDEFINED
    Write $DATA(^z),! // 0 未定义

    //布尔 非0为真, 非数字字符串为假
    If -7 {Write "true",! }
    Else { Write "false",! }
    If "abc" {Write "true",! }
    Else { Write "false",! }

    //日期
    Write $HOROLOG,! // 日期,时间 67148,55413
    Write $NOW(),! // 日期,时间.秒 67148,55413.153962305
    Write $ZDATETIME($HOROLOG, 3),! // 转为显示格式 2024-11-04 15:23:33
    Write $ZDATE(67148, 3),! // 2024-11-04
    Write $ZDATEH("2024-11-04", 3),! // 67148
    Write $ZTIME(55413),! // 15:23:33
    Write $ZTIMEH("15:23:33"),! // 55413

Variables
    /* 变量 */
    //局部变量：进程内存中，程序块访问;所有命名空间可访问
    New //所有变量未定义
    Set myvar1 = 1
    Set myvar2 = 2
    New myvar1 // 未定义myvar1
    New (myvar2) // 除myvar2外未定义
    Set %zvar3 = 3 // 公共变量
    Write myvar2,!
    Kill myvar2 //删除局部变量

    //全局变量：数据库中，命名空间访问
    Set ^myglobal = "stored in the current namespace" // 当前命名空间访问，或作映射
    Set ^%zmyglobal = "Use in all namespace" // 百分比变量：所有命名空间可访问
    Write ^|"BOE"|myglobal,! // 访问指定命名空间全局变量

    //进程私有全局变量：进程访问，结束删除 - 旨在用于大数据值，替代Temp，在进程终止时自动清理;所有命名空间可访问
    Set ^||name = "Tom"
    
    //系统变量：$前缀
    Write $JOB,! // 进程id

OperatorsAndExpressions
    /* 运算符和表达式 */
    //优先级：严格按照从左到右
    Write 1 + 2 * 3,! // 9
    Write 1 + (2 * 3),! // 7
    Write 3 && 2 = 2,! // 0

    //逻辑表达式：非零数值为真，非数字字符串为假
    //常规逻辑运算符（& 和 !） - 所有被执行
    If $$One() & $$Two() {
        Write "True",!
    }Else {
        Write "false",!
    }
    //短路逻辑运算符（&& 和 ||）- 结果确定后其他不执行
    If $$One() && $$Two() {
        Write "True",!
    }Else {
        Write "false",!
    }
    If $$One() , $$Two() { //,与&&效果一样
        Write "True",!
    }Else {
        Write "false",!
    }

    //算数运算符
    Write + "32 dollars and 64 cents",! // 32 将字符串的字符解析为数字,直到遇到不能解释为数字的字符
    Write 2936.22 + 301.45,! //  3237.67 加
    Write 2936.22 - 301.45,! // 2634.77 减
    Write 9 * 5.5,! // 49.5 乘
    Write 9 / 5.5,! // 1.636363636363636364 除
    Write "355 \ 113 = ", 355 \ 113,! // 3 整除
    Write "37 # 10 = ",37 # 10,! // 7 取余
    Write "9 ** 2 = ",9 ** 2,! // 81 指数

    //数字关系运算符
    Write 9 < 6 ,! // 0 
    Write 9 '< 6 ,! // 1 不小于
    Write 9 <= 6 ,! // 0 
    Write 9 '= 6 ,! // 1 不等于

    //逻辑比较运算符
    Write 1 & 0 ,! // 0 与
    Write 1 && 0 ,! // 0 与
    Write 1 ! 0 ,! // 1 或
    Write 1 || 0 ,! // 1 或
    Write '1 ,! //0 非

    //字符串关系运算符
    Write "abc" = "abc" ,! // 1 等于
    Write "abc" [ "a" ,! // 1 包含
    Write "abc" '[ "a" ,! // 0 不包含

    //模式匹配运算符(?)
    Set ssn="123-45-6789"
    Set match = ssn ? 3N1"-"2N1"-"4N
    Write match ,!

    //间接运算符(@):对命令行、命令或命令参数的部分或全部的动态运行时替换
    Set x = "One()"
    Do @x // One

    //宏
    Write $$$StringMacro,!

One()
    Write "One",!
    Quit 0

Two()
    Write "Two",!
    Quit 1

Commands
    /* 命令 */
    Set a = 1, b = 2
    Set (c, d) = 3
    Write:a=1 1 ,! // :后置条件表达式

    Do Two^Basics.Grammars() // 运行例程
    Do ##class(Basics.Models.Airport).DisplayAirports() // 运行类方法

    Set x = "d Two^Basics.Grammars()"
    Xecute x // 可用于动态运行

    Job One() // job 另起进程运行

    //条件语句
    Set x = 1
    If x = 1 {
        Write "x = 1",!
    } ElseIf x = 2 {
        Write "x = 2",!
    } Else {
        Write "x = ?",!
    }
                 
  	Set x =  $CASE(a, 1:"a=1", 2:"a=2", :"")

    //循环语句
    For i = 1 : 1 : 5 {
        Write i,!
    }
    For x = "a","b","c" {
        Write x,!
    }
    Set x = 0
    For {
        Quit:x=5
        Set x = x + 1
        Write x,!   
    }
    Set x = 0
    While(x < 5) {
        Set x = x + 1
        Write x,! 
    }
    Set x = 0
    Do {
        Set x = x + 1
        Write x,!
    }While(x < 5)

    //输入输出
    Set jobj = {"name":"Fred","city":"Bedrock"}
    Write jobj,! // 7@%Library.DynamicObject
    ZWrite jobj // jobj={"name":"Fred","city":"Bedrock"}  ; <DYNAMIC OBJECT>
    Set fullList = $LISTBUILD("Red","Blue","Green","Yellow")
    ZWrite fullList // fullList=$lb("Red","Blue","Green","Yellow")
    Set ^a = "a", ^a(0) = 0, ^a(1) = 1
    ZWrite ^a //^a="a" ^a(0)=0 ^a(1)=1

    //q:退出代码块 return:退出例程
    For i = 1 : 1 : 5 {
        //q:i=1
        Return:i=1
    }
    Write "END",!

main
    /* 自定义代码模块 */
    Do Add1(1, 2) // 函数调用
    Write $$Add2(1, 2),! // 过程调用
    Write c,! // 公共变量
    Set output = $$Add3(1, 2, .result) // 引用传递
    Write result,!
    Write $$Add4(1, 2, 3),! // 可变参数
    Set d(1) =1
    Set d(2) =2
    Do Add5(.d) // 可变参数引用传递


Add1(a, b)
    /* 函数 调用： Add^Basics.Grammars(1,3)*/
    Write a + b,!

Add2(a, b) [c, d] PUBLIC {
    /* 过程 c,d 为公共变量。调用： $$Add^Basics.Grammars(1,3)*/
    Set c = "c"
    Return a + b
}
Add3(a, b, &c) {
   Set c = a + b
   Return 0     
}
Add4(pars...) { // 可变参数
    Set sum = 0
    For i = 1 : 1 : pars {
        Set sum = sum + pars(i)
    }
    Quit sum
}
Add5(&pars...)
    Set sum = 0
    Set item = ""
    For {
        Set item = $ORDER(pars(1,item))
        Quit:item=""
        Set sum = sum + item
    }
    Write sum,!
```

## 2.2 事务

```java
ROUTINE Basics.Transactions
ROU        
    /* 事务 */
    Set $ZTRAP = "ERROR"
    Tstart
    Write "Transaction1 Start",!
    Set ^ZGLO(1)=100
    Set ^ZGLO=error
    Set ^ZGLO(1,1)=200 
    Tcommit 
    Write "Transaction1 Committed",!
    Quit
ERROR        
    Trollback 1
    Write !,"Transaction failed." 
    Quit
```

## 2.3 锁表

```java
ROUTINE Basics.Locks
AccessData
    // 带下标的锁表名称有性能优化！
    // 如果显式锁定^x(1)，会隐式锁定子节点，例如^x(1,2)
    /* 控制对全局变量数据的访问 */
    Lock +^MyGlobal(1) // 加锁 +^MyGlobal(1):5 超时5s
    Set ^MyGlobal(1) = 1
    Lock -^MyGlobal(1) // 解锁

SameActivity
    /* 防止程序同时运行 */
    Lock ^AppStateData("NightlyBatch"):0 // 超时值为 0 表示尝试一次，然后超时
    If '$TEST { // 超时内锁定成功,$TEST=1; 否则$TEST=0
        Write "You cannot run this routine right now."
        Write !, "This routine is currently being ru	n by user: "_^AppStateData("NightlyBatch","user")
        Quit
    }
    Set ^AppStateData("NightlyBatch")=1
    Set ^AppStateData("NightlyBatch","user")=$USERNAME
    Set ^AppStateData("NightlyBatch","starttime")=$HOROLOG
    
    //main routine activity omitted from example
    
    Kill ^AppStateData("NightlyBatch")
    Lock -^AppStateData("NightlyBatch")
```

![Locks](images\Locks.png)

## 2.4 异常

```java
ROUTINE Basics.Errors
#include %occStatus
Status
    /* 获取类方法返回的%Status。成功为1, 错误返回编码和文本信息 */
    Set newAirport = ##class(Basics.Models.Airport).%New()
	Set newAirport.Name = "Philadephia International"
	Set newAirport.Code = "PHL"
	Set tSC = newAirport.%Save()
    If $$$ISERR(tSC) { // $$$ISOK:是否成功 $$$ISERR:是否错误
        Do $SYSTEM.Status.DisplayError(tSC) // 输出到当前设备
        Set ErrorText = $SYSTEM.Status.GetErrorText() // 获取错误详细信息
        Quit
    } 

ErrorHand
    /* TRY-CATCH */
    Try {
        Set ans = 5 / 0
    } Catch errobj {
        // 捕获异常后新建实例 %Exception.SystemException
        Write errobj.Code,!, errobj.Name,!, errobj.Location,!, errobj.Data,!
        // 设置异常属性 $ZERROR
        Write $ZERROR,!
    }
    /* $ZTRAP */
    Write $$TraditionalErrorHand(),!
    Quit

TraditionalErrorHand(){
    /* $ZTRAP 老的异常处理 */
    Set $ZTRAP = "Error"
    Set ans = 5/0
    Quit ans
Error
    Set $ZTRAP = "" //避免无线循环
    Set err = $ZERROR
    Quit err
}
```

# 3 类编程

**编译类：**

```java
BOE>Do $System.OBJ.Compile("Classes.Method")

Compilation started on 11/12/2024 23:04:14
Compiling class Classes.Method
Compiling routine Classes.Method.1
Compilation finished successfully in 0.021s.
```

**部署类(删除源代码，无法修改)：**

```java
BOE>Do $system.OBJ.MakeClassDeployed("Classes.Method")
```

**重建索引：**

```java
BOE>Do ##class(classname).%BuildIndices()
```

**调用方法：**

```java
Do ##class(ClassName).MethodName(args...)
DO $CLASSMETHOD(ClassName, MethodName, args...)
```



## 3.1 持久类

```java
Class Classes.Demo Extends %RegisteredObject
{

/// 创建保存实例
ClassMethod SavePatient() As %Status
{

  // 创建一个对象并返回指向该对象的OREF
  Set person = ##class(Classes.Models.Patient).%New() 
  Set person.Name = "wanyh"
  Set person.Sex = "M"
  Set person.IDNumber = "342622199304055257"
  Set person.BirthDate = $ZDATEH("1993-04-05",3)
  Set person.HeightMeter = 1.77
  Set person.WeightKg = 53
  Set person.HomeAddress = ##class(Classes.Models.Address).%OpenId(1)

  // 流属性 ^Classes.Models.PatientS
  // %Stream.GlobalCharacter, %Stream.GlobalBinary
  // %Stream.FileCharacter, %Stream.FileBinary
  Set introduction = ##class(%Stream.GlobalCharacter).%New()
  Do introduction.Write("person's introduction!")
  Do person.Introduction.CopyFrom(introduction)

  // 保存实例
  Set status = person.%Save()
      
  // 主子表
  Set personSub = ##class(Classes.Models.PatientSub).%New()
  Set personSub.Parref = person
  Set personSub.test1 = "test1"
  Set status = personSub.%Save()

  If $$$ISERR(status) {
    Do $SYSTEM.Status.DisplayError(status)
    Quit status
  }
  Quit status
}

/// 获取实例
ClassMethod GetPatientByIDNumber(IDNumber As %String = "342622199304055257") As %String
{
  // 索引
  Set id = $ORDER(^Classes.Models.PatientI("IDNumberIndex", " "_IDNumber, ""))

  // 打开实例
  Set person = ##class(Classes.Models.Patient).%OpenId(id)
  
  // 获取属性
  Set name = person.Name
  Set birthDate = $ZDATE(person.BirthDate,3)
  Set Age = person.Age
  s HomeAddress = ##class(Classes.Models.Address).%OpenId(1)
  s Province = HomeAddress.Province

  // 流属性
  Set introduction = person.Introduction
  Write introduction.Size,! //流大小
  Write introduction.FindAt(1, "in"),! //查找字符串位置
  Do introduction.MoveTo(10) //移动到流的给定位置
  Do introduction.MoveToEnd() //移动到流的末尾
  Do introduction.Write("New Strings!") //从当前位置附加至流
  Do introduction.Rewind() //移至流的开头
  While 'introduction.AtEnd { //流末尾时true
    Set len = 10
    Write introduction.Read(.len) //读取流
  }

  // 使用引用类常量
  Write person.#CURRNTIME,! 

  // 调用实例方法
  Set bmi = person.GetBMI()

  // 对象其他操作
  //Do person.%DeleteId(id) //删除实例
  Write person.%Id(),! //对象 ID 值
  Write ##class(Classes.Models.Patient).%ExistsId(1),! //检查指定的ID是否存在
  Write person.%ClassName(1),!, $CLASSNAME(person),! //返回实例的对象
  Set NewPerson = person.%ConstructClone() //克隆对象 不同的OREF
	
  // 主子表
  Set sub = 0
  For {
    Set sub = $ORDER(^Classes.Models.PatientD(id,"ChildPatSub",sub))
    Quit:sub=""
    Set test1 = $LISTGET(^Classes.Models.PatientD(id,"ChildPatSub",sub),2)
  }
    
  Quit name_"^"_bmi
}
}
```



**`Classes.Models.Patient`**

```java
Class Classes.Models.Patient Extends %Persistent
{

/// 类常量
Parameter CURRNTIME = {$ZDATETIME($HOROLOG, 3)};

/// 姓名
Property Name As %String(MAXLEN = 500) [ Required ];

/// 性别
Property Sex As %String(DISPLAYLIST = ",Male,Female", MAXLEN = 1, VALUELIST = ",M,F") [ SqlColumnNumber = 2, SqlFieldName = Gender ];

/// 身份证号
Property IDNumber As %String(PATTERN = "18N") [ Required ];

/// 出生日期
Property BirthDate As %Date;

/// 年龄
Property Age As %Integer [ SqlComputed ];

/// 身高
Property HeightMeter As %Numeric;

/// 体重
Property WeightKg As %Numeric;

/// 家庭地址
Property HomeAddress As Classes.Models.Address;

/// 介绍
Property Introduction As %Stream.GlobalCharacter;

/// Index MainIDX On IDNumber [ Idkey ]; //将IDNumber作为表ID
Index IDNumberIndex On IDNumber [ Unique ];

/// Index for Name
Index NameIndex On Name;

/// 年龄计算
ClassMethod AgeComputation(cols As %Library.PropertyHelper) As %String
{
  Return +$HOROLOG - cols.getfield("BirthDate")
}

/// 计算BMI
/// 实例方法
Method GetBMI() As %Numeric
{
  Set bmi = ..WeightKg / (..HeightMeter**2)
  Quit bmi
}

Storage Default
{
<Data name="PatientDefaultData">
<Value name="1">
<Value>%%CLASSNAME</Value>
</Value>
<Value name="2">
<Value>Name</Value>
</Value>
<Value name="3">
<Value>Sex</Value>
</Value>
<Value name="4">
<Value>IDNumber</Value>
</Value>
<Value name="5">
<Value>BirthDate</Value>
</Value>
<Value name="6">
<Value>Age</Value>
</Value>
<Value name="7">
<Value>HeightMeter</Value>
</Value>
<Value name="8">
<Value>WeightKg</Value>
</Value>
<Value name="9">
<Value>HomeAddress</Value>
</Value>
<Value name="10">
<Value>image</Value>
</Value>
<Value name="11">
<Value>Introduction</Value>
</Value>
</Data>
<DataLocation>^Classes.Models.PatientD</DataLocation>
<DefaultData>PatientDefaultData</DefaultData>
<IdLocation>^Classes.Models.PatientD</IdLocation>
<IndexLocation>^Classes.Models.PatientI</IndexLocation>
<StreamLocation>^Classes.Models.PatientS</StreamLocation>
<Type>%Storage.Persistent</Type>
}

}
```

**`Classes.Models.Address`**

```java
Class Classes.Models.Address Extends %Persistent [ SqlTableName = PatientAddress ]
{

/// 省
Property Province As %String;

/// 市
Property City As %String;

/// 县
Property Country As %String;

Storage Default
{
<Data name="AddressDefaultData">
<Value name="1">
<Value>%%CLASSNAME</Value>
</Value>
<Value name="2">
<Value>Province</Value>
</Value>
<Value name="3">
<Value>City</Value>
</Value>
<Value name="4">
<Value>Country</Value>
</Value>
</Data>
<DataLocation>^Classes.Models.AddressD</DataLocation>
<DefaultData>AddressDefaultData</DefaultData>
<IdLocation>^Classes.Models.AddressD</IdLocation>
<IndexLocation>^Classes.Models.AddressI</IndexLocation>
<StreamLocation>^Classes.Models.AddressS</StreamLocation>
<Type>%Storage.Persistent</Type>
}

}
```

**`Classes.Models.PatientSub`**

```java
Class Classes.Models.PatientSub Extends %Persistent
{

/// 主子表
Relationship Parref As Classes.Models.Patient [ Cardinality = parent, Inverse = ChildPatSub ];

Property test1 As %String;

Storage Default
{
<Data name="PatientSubDefaultData">
<Value name="1">
<Value>%%CLASSNAME</Value>
</Value>
<Value name="2">
<Value>test1</Value>
</Value>
</Data>
<DataLocation>{%%PARENT}("ChildPatSub")</DataLocation>
<DefaultData>PatientSubDefaultData</DefaultData>
<IdLocation>^Classes.Models.PatientC("ChildPatSub")</IdLocation>
<IndexLocation>^Classes.Models.PatientSubI</IndexLocation>
<StreamLocation>^Classes.Models.PatientSubS</StreamLocation>
<Type>%Storage.Persistent</Type>
}

}
```



## 3.2 注册类

```java
Class Classes.Demo Extends %RegisteredObject
{

/// 参数传递
ClassMethod Arguments()
{
  Do ..Add(3, 4, .result) //引用传递
  Write result,!

  Do ..CreateObject(.employee) //引用传递
  ZWrite employee

  Do ..MultiArg("a", "b", , "c") //多参数传递
}

ClassMethod Add(x As %Numeric, y As %Numeric, ByRef result As %Numeric)
{
  Set result = x + y
  Quit $$$OK
}

ClassMethod CreateObject(Output newObj As Classes.Models.Patient)
{
  Set newObj = ##class(Classes.Models.Patient).%New()
  Quit $$$OK
}

ClassMethod MultiArg(args... As %String)
{
    For i = 1 : 1 : args {
        Write "Argument[", i , "]:",  $GET(args(i), "<NULL>"), !
    }
}
    
/// 使用 XData 块
ClassMethod GetXData() As %Status
{
  Set xdataID="Classes.Demo||SexDict"
  #; Set xdataID="Classes.Demo||SexJson"
  Set compiledXdata=##class(%Dictionary.CompiledXData).%OpenId(xdataID)
  Set tStream=compiledXdata.Data
  If '$ISOBJECT(tStream) Set tSC=%objlasterror Quit
  Do ##class(%XML.XPATH.Document).CreateFromStream(tStream,.tDocument)
  Do tDocument.EvaluateExpression("/table","count(row)",.tRes)
  Set count = tRes.GetAt(1).Value
  For i = 1 : 1 : count {
    Do tDocument.EvaluateExpression("/table/row["_i_"]","text()",.tRes)
    Set fieldValue = tRes.GetAt(1).Value
    Set key = $PIECE(fieldValue,"^",1)
    Set value = $PIECE(fieldValue,"^",2)
    Write key,":",value,!
  }

  #; Set dynObject = {}.%FromJSON(tStream)
  #; Write dynObject.%ToJSON()
}

/// sex 字典
XData SexDict
{
<table>
  <row>M^男</row>
  <row>F^女</row>
</table>
}

XData SexJson [ MimeType = application/json ]
{
  {
    "M": "男",
    "F": "女"
  }
}
    
}
```

## 3.3 类查询

```java
Class Classes.Demo Extends %RegisteredObject
{

/// 使用类查询
ClassMethod UseQuery()
{
  Set statement = ##class(%SQL.Statement).%New()
  Set status = statement.%PrepareClassQuery("Classes.Demo","ByName")
  Set rset = statement.%Execute("wanyh")
  While rset.%Next()
  {
    Write !, rset.%Get("Name")
  }
}

/// 基本类查询 %SQLQuery
/// d ##class(%ResultSet).RunQuery("Classes.ClassQueries", "ByName", "wanyh")
/// CALL Classes.Patient_By_Name("wanyh")
Query ByName(name As %String = "") As %SQLQuery(CONTAINID = 1, ROWSPEC = "ID:%Integer,Name,IDNumber", SELECTMODE = "RUNTIME") [ SqlName = Patient_By_Name, SqlProc ]
{
  SELECT ID, Name, IDNumber
    FROM Classes_Models.Patient
    WHERE Name =:name
}

/// 自定义类查询 %Query
/// d ##class(%ResultSet).RunQuery("Classes.Demo", "ByIDNumber", "342622199304055257")
/// CALL Classes.Patient_By_IDNumber("342622199304055257")
Query ByIDNumber(IDNumber As %String = "") As %Query(ROWSPEC = "ID:%String,Name:%String,IDNumber:%String") [ SqlName = Patient_By_IDNumber, SqlProc ]
{
}

ClassMethod ByIDNumberExecute(ByRef qHandle As %Binary, IDNumber As %String = "") As %Status
{
  Set dataList = []
	Set qHandle = dataList.%GetIterator()
	
	Set id = $ORDER(^Classes.Models.PatientI("IDNumberIndex", " "_IDNumber, ""))
	Set person = ##class(Classes.Models.Patient).%OpenId(id)
	Set name = person.Name
	Do dataList.%Push($LISTBUILD(id,name,IDNumber))
		
  Quit $$$OK
}

ClassMethod ByIDNumberFetch(ByRef qHandle As %Binary, ByRef Row As %List, ByRef AtEnd As %Integer = 0) As %Status [ PlaceAfter = ByIDNumberExecute ]
{
  If 'qHandle.%GetNext(.key,.Row) Set AtEnd=1
	Quit $$$OK
}

}

```

## 3.4 列表和数组

```java
Class Classes.Demo Extends %RegisteredObject
{

/// 列表和数组
ClassMethod ListAndArray()
{
  // 定义集合
  Set dataList = ##class(%ListOfDataTypes).%New()
  Set objList = ##class(%ListOfObjects).%New()
  Set dataArray = ##class(%ArrayOfDataTypes).%New()
  Set objArray = ##class(%ArrayOfObjects).%New()

  // 使用列表
  Do dataList.Insert("Red")
  Do dataList.Insert("Green")
  Do dataList.InsertAt("Yellow",2) //插入到指定位置
  Do dataList.SetAt("Blue",1) //修改指定位置
  Do dataList.RemoveAt(2) //删除指定位置
  For i = 1 : 1 : dataList.Count() {
    Write dataList.GetAt(i),! //获取指定位置
  }

  // 使用数组
  Do dataArray.SetAt("red","color")
  Do dataArray.SetAt("large","size")
  Do dataArray.SetAt("expensive","price")
  Do dataArray.RemoveAt("price")
  Write dataArray.GetAt("color")
  Set key = ""
  For {
    Set value = dataArray.GetNext(.key)
    Quit:key=""
    Write !,key," = ",value
  }
    
  // Lists
  Set mail = $LISTBUILD("One Memorial Drive", "Cambridge", "MA", "02142")
  Write !,$LISTLENGTH(mail)
  Write !,$LIST(mail, 2)
  Write !,$LIST(mail, *-1)
  Write !,$LISTFIND(mail, "MA")
  Write !,$LISTTOSTRING(mail, "^")
  Set pmail = $LISTFROMSTRING("One Memorial Drive^Cambridge^MA^02142", "^")
}

}

```
